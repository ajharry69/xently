plugins {
    id 'com.android.library'
    id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin' // apply below kotlin-kapt
}

android {
    compileSdk 31

    defaultConfig {
        minSdk 21
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += [
                "-Xopt-in=kotlin.time.ExperimentalTime",
                "-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi",
                "-Xopt-in=androidx.compose.material.ExperimentalMaterialApi",
                "-Xopt-in=com.google.accompanist.permissions.ExperimentalPermissionsApi",
        ]
        useIR = true
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }

//    packagingOptions {
//        pickFirsts += "build-data.properties"
//    }
}

dependencies {
    implementation project(path: ":common")
    implementation project(path: ":data")
    implementation project(path: ":data:source:local")
    implementation project(path: ":data:source:remote")
    implementation project(path: ":feature")

    implementation "com.google.android.libraries.maps:maps:3.1.0-beta"
    implementation 'com.google.maps.android:maps-v3-ktx:3.2.0'

    constraints {
        // Volley is a transitive dependency of maps
        implementation("com.android.volley:volley:1.2.0") {
            because("Only volley 1.2.0 or newer are available on maven.google.com")
        }
    }

    // DI
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    // This is a transitive dependency to remote data source that's currently only needed by:
    //  1. `sendRequest(...)` function declared in the remote data source module.
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}