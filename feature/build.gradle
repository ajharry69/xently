plugins {
    id 'com.android.library'
    id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin' // apply below kotlin-kapt
}

android {
    compileSdk 31

    defaultConfig {
        minSdk 21
        targetSdk 31

        consumerProguardFiles "consumer-rules.pro"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // The following argument makes the Android Test Orchestrator run its
        // "pm clear" command after each test invocation. This command ensures
        // that the app's state is completely cleared between tests.
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        animationsDisabled = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        useIR = true
        freeCompilerArgs += [
                "-opt-in=kotlin.RequiresOptIn",
                "-opt-in=kotlin.time.ExperimentalTime",
                "-opt-in=com.google.accompanist.permissions.ExperimentalPermissionsApi",
        ]
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    packagingOptions {
        resources {
            pickFirsts += ['build-data.properties']
        }
    }
}

dependencies {
    implementation project(path: ":common")
    // Only needed by `TaskResult` flow utility
    implementation project(path: ':data')
    implementation project(path: ':data:source:remote')
    implementation project(path: ':data:source:local')

    api 'com.google.android.material:material:1.6.0'

    // DI
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    implementation 'com.google.maps.android:maps-compose:2.1.1'
    // Make sure to also include the latest version of the Maps SDK for Android
    implementation 'com.google.android.gms:play-services-maps:18.0.2'

    implementation 'com.google.android.gms:play-services-location:19.0.1'

    // Navigation
    api "androidx.navigation:navigation-compose:$navigation_version"

    api "androidx.paging:paging-compose:1.0.0-alpha14"
    api 'androidx.hilt:hilt-navigation-compose:1.0.0'
    // https://developer.android.com/jetpack/compose/libraries#viewmodel
    api "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_viewmodel_version"
    api "com.google.accompanist:accompanist-permissions:0.20.0"
    api 'com.google.accompanist:accompanist-placeholder-material:0.24.9-beta'
    implementation 'com.google.accompanist:accompanist-swiperefresh:0.24.9-beta'
    api "androidx.compose.material:material:$compose_version"
    api "androidx.compose.material:material-icons-extended:$compose_version"
    api "androidx.compose.ui:ui-tooling-preview:$compose_version"
    debugApi "androidx.compose.ui:ui-tooling:$compose_version"

    testImplementation project(path: ':feature:tests:unit')
    androidTestUtil 'androidx.test:orchestrator:1.4.1'
}