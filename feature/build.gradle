plugins {
    id 'com.android.library'
    id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin' // apply below kotlin-kapt
}

android {
    compileSdk 31

    defaultConfig {
        minSdk 21
        targetSdk 31

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        useIR = true
        freeCompilerArgs += [
                "-Xopt-in=kotlin.RequiresOptIn",
                "-Xopt-in=kotlin.time.ExperimentalTime",
                "-Xopt-in=com.google.accompanist.permissions.ExperimentalPermissionsApi",
        ]
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    packagingOptions {
        resources {
            pickFirsts += ['build-data.properties']
        }
    }
}

dependencies {
    implementation project(path: ":common")
    // Only needed by `TaskResult` flow utility
    implementation project(path: ':data')
    implementation project(path: ':data:source:remote')
    implementation project(path: ':data:source:local')

    api 'com.google.android.material:material:1.5.0'

    // DI
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    api "com.google.android.libraries.maps:maps:3.1.0-beta"
    implementation 'com.google.maps.android:maps-v3-ktx:3.3.0'

    constraints {
        // Volley is a transitive dependency of maps
        implementation("com.android.volley:volley:1.2.0") {
            because("Only volley 1.2.0 or newer are available on maven.google.com")
        }
    }

    implementation 'com.google.android.gms:play-services-location:19.0.1'

    // Navigation
    api "androidx.navigation:navigation-compose:$navigation_version"

    api "androidx.paging:paging-compose:1.0.0-alpha14"
    api 'androidx.hilt:hilt-navigation-compose:1.0.0'
    // https://developer.android.com/jetpack/compose/libraries#viewmodel
    api "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_viewmodel_version"
    api "com.google.accompanist:accompanist-permissions:0.20.0"
    api 'com.google.accompanist:accompanist-placeholder-material:0.24.3-alpha'
    implementation "com.google.accompanist:accompanist-swiperefresh:0.24.3-alpha"
    api "androidx.compose.material:material:$compose_version"
    api "androidx.compose.material:material-icons-extended:$compose_version"
    api "androidx.compose.ui:ui-tooling-preview:$compose_version"
    debugApi "androidx.compose.ui:ui-tooling:$compose_version"

    testImplementation project(path: ':feature:tests:unit')
}